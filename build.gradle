import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'java'
    id 'maven'
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "com.github.johnrengelman.shadow" version '5.1.0'
//    id 'org.kordamp.gradle.jdeps' version '0.7.1-SNAPSHOT'
}

// jitpack
group = 'com.github.kotlin-graphics'

ext{
    moduleName = 'com.github.kotlin_graphics.kool'
    lwjgl_version = "3.2.2"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib".toString()

    ["", "-jemalloc"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
        runtime "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

compileKotlin {

    kotlinOptions {
        freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
        jvmTarget = "1.8"
    }
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions{
        jvmTarget = "1.8"
//        javaParameters = true
    }
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath,]
        classpath = files()
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest.attributes(
//            'Automatic-Module-Name': moduleName
            'Multi-Release': 'true'
    )
}

//jdepsReport {
//    multiReleaseJars = [
//            'lwjgl-3.2.2.jar': 12,
//////            'lwjgl-*': 12,
//////            'lwjgl-3.2.2.jar': 12
//    ]
//}